apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = "1.0.1"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/wanderson7/statusviewandroid'
def gitUrl  = 'https://github.com/wanderson7/statusviewandroid.git'
group       = "com.wandersonsantos"
install {
    repositories.mavenInstaller {
        // para gerar o arquivo POM.xml
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com/wanderson7/statusviewandroid'
                description = 'library to status view'
                url siteUrl

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'wanderson7'
                        name 'Wanderson Santos'
                        email 'wandeerson7@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "status-view"
        name = "status-view"
        websiteUrl = siteUrl
        vcsUrl     = gitUrl
        licenses   = ['MIT']
        publish    = true
    }
}

dependencies {
    // compile 'groupId:artifactId:version'
    // groupId    = com.android.support
    // artifactId = appcompat-v7
    // version    = 27.1.1
    implementation 'com.android.support:appcompat-v7:27.1.1'
}
